/* REXX */
Address ISPExec

  'vget (zapplid)'
  /*
  "Control Errors Return"
  */
  parse value '' with null
  /* ----------------------------------------------------- *
  | Initialise the application named THISAPP              |
  * ----------------------------------------------------- */
  parse source x1 x2 x3 x4 exec .
  if exec = '?' then do
    x = listdsi(x4 'FILE')
    exec = sysdsname
  end

  hlq = translate(exec,' ','.')
  hlq = subword(hlq,1,words(hlq)-1)
  hlq = translate(hlq,'.',' ')

  exec = "'"exec"'"
  panels = "'"hlq".PANELS'"
  config = "'"hlq".CONFIG'"
  solutions = "'"hlq".SOLVE.REXX" /* No closing ' so we can add (*/

  if zapplid /= 'ZAOC' then do
    if panels /= null then
    'libdef ispplib dataset id('panels') stack'
    if exec /= null then
    Address TSO 'altlib act application(exec) dataset('exec')'
  end

  if zapplid <> 'ZAOC' then do
    "Select CMD(%zaoc) Newappl(ZAOC) Passlib"
    if panels /= null then
    'libdef ispplib'
    if exec /= null then
    Address TSO 'altlib deact application(exec)'
    exit 0
  end

  /* read config */
  curlpath = ''
  session = ''
  inputfiles = ''
  call read_config
  /* Now we start our main panel. */


  'TBOpen zaocmain Library(isptabl) Write Share'
  if rc > 0 then do
    if rc = 8 then do
      /* Create table, always add current year, day01*/
      "TBCreate zaocmain",
        "Names(year day part status)",
        "Library(isptabl) Write Share"
      "TBAdd zaocmain"
      "TBSAVE zaocmain REPLCOPY LIBRARY(isptabl)"
    end
  end
  zcmd = null

  year_top = 0
  'tbsort zaocmain fields(year,n,a,day,n,a)'
  'tbtop zaocmain'
  do forever
    parse value '' with zsel zcmd row
    'TBTop zaocmain'
    'tbskip zaocmain number('year_top')'
    'tbdispl zaocmain panel(zaocmain)'

    if rc > 4 then leave

    repo_top = ztdtop
    if row = 0 then zsel = null
    if row <> null then
    if row > 0 then do
      'TBTop zaocmain'
      'TBSkip zaocmain Number('row')'
    end
    Select
      When abbrev('ADD',zcmd,2) = 1 then call Add_puzzle
      When abbrev('SOR',zcmd,2) = 1 then do
        'tbsort zaocmain fields(year,n,a,day,n,a)'
      end
      Otherwise nop
    End
    ys = substr(year,3,2)
    rx = solutions"(Y"ys"D"day"P"part")'"
    if zsel = 'E' then do
      'Edit dataset('rx')'
    end
    if zsel = 'R' then do
      Address TSO
      "EX "rx
      Address ISPExec
      if POS("Input file",status) = 1
      then do
        parse var status "Input file downloaded at" t "on" d
        t = strip(t)
        d = strip(d)
        status = "1 at" TIME() "on" DATE("E") "start at" t "on" d
      end
      else if POS("Solved in",status) = 0 then do
        parse var status count "at" t1 "on" d1 "start at" t "on" d
        t = strip(t)
        d = strip(d)
        count = count + 1
        t1 = strip(TIME())
        d1 = strip(DATE("E"))
        status = count "at" t1 "on" d1 "start at" t "on" d
      end
      "TBPUT zaocmain"
      "TBSAVE zaocmain REPLCOPY LIBRARY(isptabl)"
    end
    if zsel = 'S' then do
      if POS('start at',status) > 0 then do
        parse var status count "at" t1 "on" d1 "start at" t "on" d
        t = strip(t)
        d = strip(d)
        if count > 1
        then tries = "tries"
        else tries = "try"
        status = "Solved in" strip(count) tries
        status = status d"@"t" > "strip(DATE("E"))"@"strip(TIME())
        "TBPUT zaocmain"
        "TBSAVE zaocmain REPLCOPY LIBRARY(isptabl)"
      end
    end
    if zsel = 'D' then do
      status = 'Tried'
      "TBDELETE zaocmain"
      "TBSAVE zaocmain REPLCOPY LIBRARY(isptabl)"
    end
  end

  'TBCLOSE zaocmain'

  Exit 0

Add_puzzle:

  'Addpop row(4) column(12)'
  'Display Panel(addpuzz)'
  'Rempop'
  /* Now we need to go out to adventofcode.com
  and get the puzzle for this day.. and the
  input file....                       */
  cmd = curlpath
  y = yyyy
  d = dd
  cmd = cmd "'https://adventofcode.com/"y"/day/"d"/input'"
  cmd = cmd "-H 'Accept: text/html'"
  cmd = cmd "-H 'Cookie: session="session";' -k > "inputfiles"/y"y"d"d
  x = bpxwunix(cmd,,so.,se.)
  if x = 0 then do
    year = yyyy
    day  = dd
    status = "Input file downloaded at" TIME() "on" DATE("E")
    part   = 1
    'tbadd zaocmain'
    "TBSAVE zaocmain REPLCOPY LIBRARY(isptabl)"

    part   = 2
    'tbadd zaocmain'
    "TBSAVE zaocmain REPLCOPY LIBRARY(isptabl)"
  end
  /* create template solution */
  Address TSO
  ddname1 = "ZAOC#RX1"
  ddname2 = "ZAOC#RX2"
  ys = substr(y,3,2)
  s.1 = "/*--------------------------------------------[REXX]"
  s.2 = "| Made with ZAOC by wizard@zdevops.com             |"
  s.3 = "+-------------------------------------------------*/"
  s.4 = 'say "Advent of Code' year 'day' day'"'
  s.5 = "x = bpxwunix('cat "inputfiles"/y"y"d"d"',,file.,se.)"
  s.6 = "do i = 1 to file.0"
  s.7 = "  say file.i"
  s.8 = "end"
  s.0 = 8
  "Alloc f("ddname1") shr reuse ds("solutions"(Y"ys"d"d"p1)')"
  'Execio * diskw' ddname1 '(finis stem s.'
  'Free  f('ddname1')'
  "Alloc f("ddname2") shr reuse ds("solutions"(Y"ys"d"d"p2)')"
  'Execio * diskw' ddname2 '(finis stem s.'
  'Free  f('ddname2')'
  Address ISPExec
  return

read_config: procedure,
 expose config curlpath session inputfiles
 parse var config
 Address TSO
 "Alloc f("ddname") shr reuse ds("config")"
 'Execio * diskr' ddname '(finis stem cfg.'
 'Free  f('ddname')'
 Address ISPExec
 do i = 1 to cfg.0
   if left(cfg.i,1) = '*' then iterate
   Select
     When translate(word(cfg.i,1)) = 'CURL_PATH' then
          curlpath  = word(cfg.i,3)
     When translate(word(cfg.i,1)) = 'SESSION' then
          session  = word(cfg.i,3)
     When translate(word(cfg.i,1)) = 'INPUTFILES' then
          inputfiles  = word(cfg.i,3)
     Otherwise nop
   End
 end
 return


/* To add for Completion Post/Check

curl 'https://adventofcode.com/2015/day/1/answer'
     -X POST
     -H 'Accept: text/html
     -H 'Content-Type: application/x-www-form-urlencoded'
     -H 'Cookie: session=555...
     --data-raw 'level=1&answer=<answer>'

*/
